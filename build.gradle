buildscript {
	repositories {
		mavenCentral()
	    maven {
	      url "https://plugins.gradle.org/m2/"
	    }
	}
	dependencies {
		classpath "gradle.plugin.org.eclipse.virgo.bundlor:bundlor-plugin:0.2"
		classpath "gradle.plugin.com.eclipsesource.dockerizor:dockerizor:0.6.3"
	}
}

ext.virgoProjects = [
	project(':game-api'),
	project(':jenova'),
	project(':game-engine'),
	project(':commands'),
	project(':server'),
	project(':client'),
]

ext.dockerProjects = [
	project(':runtime-only'),
	project(':app'),
]

configure(virgoProjects) {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'eclipse-wtp'

	version = '0.1.0.BUILD-SNAPSHOT'

	repositories { 
	    mavenLocal()
	    mavenCentral()
	}

	compileJava {
		options.encoding = 'utf-8'
		options.fork = true
		doFirst {
			options.forkOptions.with {
				executable = 'java'
				jvmArgs = [
					'-cp',
					configurations.ecj.asPath,
					'org.eclipse.jdt.internal.compiler.batch.Main',
					'-source',
					'1.8',
	                '-target',
	                '1.8',
					'-warn:-unused'
				]
			}
		}
	}

	compileTestJava {
		options.encoding = 'utf-8'
		options.fork = true
		doFirst {
			options.forkOptions.with {
				executable = 'java'
				jvmArgs = [
					'-cp',
					configurations.ecj.asPath,
					'org.eclipse.jdt.internal.compiler.batch.Main',
					'-source',
					'1.8',
					'-target',
					'1.8',
					'-warn:-unused'
				]
			}
		}
	}

	eclipse {
		jdt {
			sourceCompatibility = 1.8
			targetCompatibility = 1.8
		}

		project {
		  	natures 'org.eclipse.virgo.ide.facet.core.bundlenature', 'org.eclipse.wst.common.project.facet.core.nature'
			buildCommand 'org.eclipse.virgo.ide.bundlor.core.builder'
		}

		wtp {
		    facet {
				facets = []
				facet name: 'jst.java', version: '1.8'
				facet name: 'org.eclipse.virgo.server.bundle', version: '1.0'
			}
		}
	}

	configurations {  ecj { } }
}

configure(virgoProjects) {
	apply plugin: 'org.eclipse.virgo.bundlor'
	
	task('addProjectVersionToGradlePropertiesTask', type: Copy) {
		ext.outputDir = new File(buildDir, 'bundlor-properties')
		inputs.file project.rootProject.file('gradle.properties')
		outputs.dir ext.outputDir
		from project.rootProject.file('gradle.properties')
		into ext.outputDir
	}

	task('mergeClassesAndResources', type: Copy, dependsOn: 'compileJava') {
		ext.bundlorInputPath = new File(buildDir, 'bundlor-inputPath')
		inputs.file sourceSets.main.output.classesDir
		inputs.file sourceSets.main.resources
		outputs.dir ext.bundlorInputPath
		from sourceSets.main.output.classesDir
		from sourceSets.main.resources
		into ext.bundlorInputPath
	}

	project.tasks.'bundlor'.dependsOn('addProjectVersionToGradlePropertiesTask', 'mergeClassesAndResources')

	bundlor {
		ext.bundlorInputPath = new File(buildDir, 'bundlor-inputPath')
		project.tasks['bundlor'].inputs.file ext.bundlorInputPath
		manifestTemplatePath = "template.mf"
		propertiesPath = new File("${project.buildDir}/bundlor-properties/gradle.properties")
		inputPath = ext.bundlorInputPath
		failOnWarnings = false
	}
}

configure(dockerProjects) {
	// add the build task 'clean'
	apply plugin: 'base'
	
	// add the build task 'dockerize'
	apply plugin: "com.eclipsesource.dockerizor"

	repositories {
		mavenCentral()
	}

	dockerizor {
		maintainer = 'Florian Waibel <fwaibel@eclipsesource.com>'

		javaImage = 'java:openjdk-8u91-jre'

	 	hudsonJobName = '3.7.0.Milestones'
		virgoFlavour = 'VTS'
		virgoVersion = 'latest'

		removeAdminConsole = false

	    createLocalCopy = true

	    postDockerizeHook = { task ->
	        project.logger.info "Adding nashorn packages to configuration/java-server.profile"
	        task.RUN "sed -i 's/org.xml.sax.helpers/org.xml.sax.helpers,\\\\\\n jdk.nashorn.api.scripting/' ${project.dockerizor.virgoHome}/configuration/java-server.profile"
	        task.RUN "sed -i 's/ sun.*/ sun.*,\\\\\\n jdk.*/' ${project.dockerizor.virgoHome}/configuration/java-server.profile"
	    }

	    dockerize.dependsOn configurations.repositoryUsr
	}

	dependencies {
	    endorsed files('libs/nashorn.jar')

		repositoryExt group: "com.fasterxml.jackson.core", name: "jackson-core", version: jacksonCoreVersion
		repositoryExt group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: jacksonAnnotationsVersion
		repositoryExt group: "com.fasterxml.jackson.core", name: "jackson-databind", version: jacksonDatabindVersion
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
